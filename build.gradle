import java.time.Year

buildscript {
    repositories {
        jcenter()
        maven {
            name = "Forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "Sonatype"
            url = 'https://oss.sonatype.org/content/groups/public'
        }
    }
    dependencies {
        classpath 'com.matthewprenger:CurseGradle:1.0-SNAPSHOT'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

apply from: 'https://gist.githubusercontent.com/UpcraftLP/0a98a7dfe913bd3e99073efa7ad0353b/raw'
//Only edit below this line, the above code adds and enables the nessasary things for Forge to be setup.

def username = "${modid}"
if (project.hasProperty('dev_mail')) {
    username = "${dev_mail}"
}
else if(project.hasProperty('dev_username')) {
    username = "${dev_username}"
}

def buildVersion = project.mod_version + ext.buildName
version = "${project.mc_version}-" + buildVersion
group = project.maven_group // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = project.modid
tasks.build.doFirst { println("Current version is ${modid}-${version}") }

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8 // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    maven {
        //JEI files
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "http://modmaven.k-4u.nl"
    }

    maven {
        //hwyla
        name = "TehNut"
        url = "https://tehnut.info/maven"
    }

    maven {
        //fallback for almost everything, this is CurseForge :P
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }

    maven {
        //get dependencies from github
        name = "JitPack"
        url = "https://jitpack.io"
    }
}

minecraft {
    version = "${project.mc_version}-${project.forge_version}"
    if(project.hasProperty('runDir')) {
        runDir = project.runDir
    }
    else if(file('../run').exists()) {
        runDir = "../run"
    }
    else
    {
        runDir = "run"
    }

    mappings = project.forge_mappings
    makeObfSourceJar = false
	
    replace "@VERSION@", project.mod_version
    def fingerPrintKey = project.hasProperty('public_key') ? findProperty('public_key').replace(":", "").toLowerCase(Locale.ROOT) : "undefined"
    replace "@FINGERPRINTKEY@", fingerPrintKey
    replace "@UPDATEJSON@", project.hasProperty('update_json') ? findProperty('update_json') : ""
    replaceIn project.reference_class

    //auto-configure the run arguments
    if(project.hasProperty('dev_uuid')) {
        clientRunArgs += "--uuid=${dev_uuid}"
    }
    else {
        clientRunArgs += "--username=${username}"
        if(project.hasProperty('dev_password')) clientRunArgs += "--password=${dev_password}"
    }
    
    //skip the screen to confirm that you want to load a world with missing registry entries
    serverJvmArgs += "-Dfml.doNotBackup=true"
    clientJvmArgs += "-Dfml.doNotBackup=true"
    
    // skip having to confirm on server
    serverJvmArgs += "-Dfml.queryResult=confirm"
    
    // disable server gui
    serverRunArgs += "nogui"
    
    //skip jansi warnings in the log
    serverJvmArgs += '-Dlog4j.skipJansi=true'
    clientJvmArgs += '-Dlog4j.skipJansi=true'

    useDepAts = true //PickleLib needs some ATs
}

dependencies {

    // compile against the JEI API but do not include it at runtime
    deobfProvided "mezz.jei:jei_${project.mc_version}:${project.jei_version}:api"
    //JEI at runtime (no build dependency, this is for testing purposes ONLY!)
    runtime "mezz.jei:jei_${project.mc_version}:${project.jei_version}"

    //PickleLib, from CurseForge
    compile "picklelib:picklelib:${project.mc_version}:${project.picklelib_version}"
    //ExplosiveFishing, from CurseForge, for testing compatibility
    runtime "explosivefishing:explosivefishing:${project.mc_version}:${project.explosivefishing_version}"

    deobfProvided "mcp.mobius.waila:Hwyla:${hwyla_version}:api"
    runtime "mcp.mobius.waila:Hwyla:${hwyla_version}"
}

compileJava {
    options.encoding = 'UTF-8'
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}


// API jar
task apiJar(type: Jar) {
    from "LICENSE"
    from (sourceSets.api.allJava) {
        include 'com/github/teamrapture/aquatic/api/**'
    }
    manifest.mainAttributes(
            "Built-On-Java": "${System.getProperty('java.vm.version')} + (${System.getProperty('java.vm.vendor')})",
            "Implementation-Title": project.name + '-api',
            "Implementation-Version": project.version,
            "Maven-Artifact":"${project.group}:${project.archivesBaseName}:${project.version}:api",
            "Built-On": "${mc_version}-${forge_version}"
    )
    classifier = 'api'
}

// Javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    from "LICENSE"
    from javadoc.destinationDir
    manifest.mainAttributes(
            "Built-On-Java": "${System.getProperty('java.vm.version')} + (${System.getProperty('java.vm.vendor')})",
            "Implementation-Title": project.name + '-javadoc',
            "Implementation-Version": project.version,
            "Maven-Artifact":"${project.group}:${project.archivesBaseName}:${project.version}:javadoc",
            "Built-On": "${mc_version}-${forge_version}"
    )
    classifier = 'javadoc'
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

jar {
    from "LICENSE"
    from sourceSets.api.output
    manifest.mainAttributes(
            "Implementation-Title": project.name,
            "Implementation-Version": "${project.mod_version}",
            "Built-On": "${project.mc_version}-${project.forge_version}",
            "Maven-Artifact":"${project.group}:${project.archivesBaseName}:${project.version}"
            //"FMLAT": "${project.mod_name}_at.cfg"
    )
    classifier '' //this is the main jar!
}

//sign Jar
task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('sign_keyStore')
    }
    keyPass = findProperty('sign_keyPass')
    keyStore = findProperty('sign_keyStore')
    storePass = findProperty('sign_storePass')
    alias = findProperty('sign_alias')

    inputFile = jar.archivePath
    outputFile = jar.archivePath
}
tasks.build.dependsOn signJar, apiJar, javadocJar
tasks.curseforge.dependsOn build, publish
tasks.curseforge.mustRunAfter build, signJar
tasks.publish.mustRunAfter build, signJar

artifacts {
    archives apiJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact apiJar
            artifact javadocJar
        }
    }

    repositories {
        mavenLocal()
    }
}

def curseRelations = { //add curseforge-slug here
    optionalLibrary 'explosivefishing' //support for the IFishable interface
}

curseforge {
    if (project.hasProperty('curse_key'))
        apiKey = project.curse_key


    if(project.hasProperty('curse_id')) {
        project {
            id = project.curse_id
            changelogType = 'markdown'
            changelog = "${project.ext.changeLog}"
            releaseType = project.release_type

            relations curseRelations

            addArtifact javadocJar
            addArtifact apiJar
            if(project.hasProperty('core_package')) addArtifact coreJar

            //addGameVersion '1.12.2' //automatically determined by the CurseGradle plugin

            mainArtifact(jar) {
                displayName = "${project.mod_name} v${project.mod_version} MC${mc_version}"
            }
        }
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version,
                'mod_name':project.mod_name, 'mod_description':project.mod_description,
                'url':project.url, 'update_url':project.update_json, 'year': Year.now().getValue()
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    //ATs
    rename '(.+_at.cfg)', 'META-INF/$1'
}
